buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

group = 'de.oceanlabs.mcp'
version = '3.7.0'
targetCompatibility = '1.6'
sourceCompatibility = '1.6'

repositories {
    mavenCentral()
}

jar {
    manifest.attributes('Main-Class': 'RetroGuard')
}
shadowJar {
    classifier 'fatjar'
    manifest.attributes('Main-Class': 'RetroGuard')
}

artifacts {
    archives jar
    archives shadowJar
}

dependencies {
    compile 'net.sf.jopt-simple:jopt-simple:4.5'
    compile 'com.google.code.gson:gson:2.2.4'
}
configurations { deployJars }
uploadArchives {
    repositories {
        add project.repositories.mavenLocal()
    }
    repositories.mavenDeployer {
        configuration = configurations.deployJars

        if (project.hasProperty("filesmaven")) {
            logger.info('Publishing to files server')
            repository(url: project.filesmaven.url) {
                authentication(userName: project.filesmaven.username, privateKey: project.filesmaven.key)
            }
        } else {
            logger.info('Publishing to repo folder')
            repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
        }
        pom.project {
            name project.archivesBaseName
            packaging 'jar'
            description 'The GPL version of the RetroGuard library. Modified by the MCP team'
            url 'https://github.com/ModCoderPack/Retroguard'

            scm {
                url 'https://github.com/ModCoderPack/Retroguard'
                connection 'scm:git:git://github.com/ModCoderPack/Retroguard.git'
                developerConnection 'scm:git:git@github.com:ModCoderPack/Retroguard.git'
            }

            issueManagement {
                system 'github'
                url 'https://github.com/ModCoderPack/Retroguard/issues'
            }

            licenses {
                license {
                    name 'GNU GENERAL PUBLIC LICENSE'
                    url 'http://www.retrologic.com/rg-gnu-gpl.html'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'Fesh0r'
                    name 'Fesh0r'
                    roles { role 'developer' }
                }
                developer {
                    id 'Searge'
                    name 'Searge'
                    roles { role 'developer' }
                }
            }
        }
    }
}
